function Boss_Init(entity: entity, id: string, name: string, hp: number, barColor: color):
    set scoreboard tags of {_entity} to "boss"
    set {boss::%{_id}%} to {_entity}
    set {boss::%{_id}%::uuid} to uuid of {_entity}
    
    # Thêm vào danh sách active để chạy loop update bar
    if {active_bosses::*} does not contain {_id}:
        add {_id} to {active_bosses::*}
        add 1 to score of "count_%{_id}%" for objective with id "count_boss"
    # --- Setup Entity Stats ---
    # Gán tag để nhận diện ngược lại từ Entity -> ID nếu cần
    add "boss" to scoreboard tags of {_entity}
    add "%{_id}%" to scoreboard tags of {_entity}
    
    set max health of {_entity} to {_hp}
    set health of {_entity} to {_hp}
    set custom name of {_entity} to formatted {_name}

    # --- Setup BossBar ---
    set {_barID} to "bossbar.%{_id}%"
    # Tạo bossbar với ID tương ứng
    set {boss::%{_id}%::bar} to bossbar named {_barID} with title formatted {_name} with color {_barColor} with style segmented 10 with progress 100

    add all players to {boss::%{_id}%::bar}

function Boss_SetLimits(id: string, idle1: int, idle2: int, rampage: int):
    set {boss::%{_id}%::idle_time} to 0
    set {boss::%{_id}%::rampage_count} to 0
    set {boss::%{_id}%::cfg::idle1} to {_idle1}
    set {boss::%{_id}%::cfg::idle2} to {_idle2}
    set {boss::%{_id}%::cfg::rampage} to {_rampage}

function Boss_SetMessages(id: string, msgIdle1: string, msgIdle2: string, msgRampage: string):
    set {boss::%{_id}%::cfg::msg_idle1} to formatted {_msgIdle1}
    set {boss::%{_id}%::cfg::msg_idle2} to formatted {_msgIdle2}
    set {boss::%{_id}%::cfg::msg_rampage} to formatted {_msgRampage}

function Boss_ReduceCooldown(id: string, cd.index: integer, multiplier: number = 1):
    if {boss::%{_id}%::cooldown%{_cd.index}%} is set:
        remove {_multiplier} from {boss::%{_id}%::cooldown%{_cd.index}%}

function Boss_Cleanup(id: string):
    delete bossbar {boss::%{_id}%::bar}
    remove {_id} from {active_bosses::*}
    delete {boss::%{_id}%::*}
    ForceLoadRemove({boss::%{_id}%}, 33)
    set score of "count_%{_id}%" for objective with id "count_boss" to 0

function deleteEntity(entity: entity):
    loop all entities:
        if loop-entity is {_entity}:
            delete loop-entity
            stop

every 1 tick:
    loop {active_bosses::*}:
        set {_id} to loop-value
        set {_entity} to {boss::%{_id}%}

        if {_entity} is alive:    
            set {_prog} to (health of {_entity} / max health of {_entity}) * 100
            set bar progress of {boss::%{_id}%::bar} to {_prog}
            
            add all players to {boss::%{_id}%::bar}
        
every 1 second:
    loop {active_bosses::*}:
        set {_id} to loop-value
        set {_entity} to {boss::%{_id}%}
        
        if {_entity} is alive:
            ForceLoadRemove({_entity}, 33) 
            ForceLoadAdd({_entity}, 16)
            
            # --- Xử lý Idle Time ---
            add 1 to {boss::%{_id}%::idle_time}
            
            # Check Idle 1 (Chỉ chạy nếu có cài đặt limit)
            if {boss::%{_id}%::cfg::idle1} is set:
                if {boss::%{_id}%::idle_time} = {boss::%{_id}%::cfg::idle1}:
                    heal {_entity} by max health of {_entity}
                    if {boss::%{_id}%::cfg::msg_idle1} is set:
                        broadcast {boss::%{_id}%::cfg::msg_idle1}

            # Check Idle 2
            if {boss::%{_id}%::cfg::idle2} is set:
                if {boss::%{_id}%::idle_time} >= {boss::%{_id}%::cfg::idle2}:
                    if {boss::%{_id}%::cfg::msg_idle2} is set:
                        broadcast {boss::%{_id}%::cfg::msg_idle2}
                    Boss_Cleanup({_id})
                    deleteEntity{_entity}
            
            # --- Xử lý Rampage ---
            if {boss::%{_id}%::cfg::rampage} is set:
                if {boss::%{_id}%::rampage_count} >= {boss::%{_id}%::cfg::rampage}:
                    if {boss::%{_id}%::cfg::msg_rampage} is set:
                        broadcast {boss::%{_id}%::cfg::msg_rampage}
                    Boss_Cleanup({_id})
                    deleteEntity{_entity}
        else:
            Boss_Cleanup({_id})

function ForceLoadAdd(e: entity, r: integer):
    if {_e} is set:
        set {_loc} to location of {_e}
        set {_x} to round(x-coord of {_loc})
        set {_z} to round(z-coord of {_loc})
        
        set {_minX} to {_x} - {_r}
        set {_minZ} to {_z} - {_r}
        set {_maxX} to {_x} + {_r}
        set {_maxZ} to {_z} + {_r}
        
        execute console command "forceload add %{_minX}% %{_minZ}% %{_maxX}% %{_maxZ}%"

function ForceLoadRemove(e: entity, r: integer):
    if {_e} is set:
        set {_loc} to location of {_e}
        set {_x} to round(x-coord of {_loc})
        set {_z} to round(z-coord of {_loc})
        
        set {_minX} to {_x} - {_r}
        set {_minZ} to {_z} - {_r}
        set {_maxX} to {_x} + {_r}
        set {_maxZ} to {_z} + {_r}
        
        execute console command "forceload remove %{_minX}% %{_minZ}% %{_maxX}% %{_maxZ}%"

on vehicle enter:
    if event-entity has scoreboard tag "boss":
        cancel event
